module Basics where

open import Agda.Primitive public

--------------------------------------------------------------------------------

data âŠ¥ {l : Level} : Set l where

data Unit {l : Level} : Set l where
  tt : Unit

record Î£ {l k} {A : Set l} (B : A â†’ Set k) : Set (l âŠ” k) where
  constructor _,_
  field
    fst : A
    snd : B fst
open Î£ public


infixr 10 _,_

_Ã—_ : {l k : Level} (A : Set l) (B : Set k) â†’ Set (l âŠ” k)
A Ã— B = Î£ {A = A} (Î» _ â†’ B)



--------------------------------------------------------------------------------

-- Equivalence Relations
record EqRel {ls lr : Level} {A : Set ls} (R : A â†’ A â†’ Set lr) : Set (ls âŠ” lr) where
  field
    refl  : {a : A} â†’ R a a
    sym   : {a b : A} â†’ R a b â†’ R b a
    trans : {a b c : A} â†’ R a b â†’ R b c â†’ R a c

open EqRel public

module EqRelReason {l l' : Level} {A : Set l} {_==_ : A â†’ A â†’ Set l'}
                   (eqr : EqRel _==_) where
  infix  3 _âˆ
  infixr 2 _â‰ˆâŸ¨âŸ©_ _â‰ˆâŸ¨_âŸ©_
  infix  1 begin_

  begin_ : âˆ€{x y : A} â†’ x == y â†’ x == y
  begin_ p = p

  _â‰ˆâŸ¨âŸ©_ : âˆ€ (x {y} : A) â†’ x == y â†’ x == y
  _ â‰ˆâŸ¨âŸ© p = p

  _â‰ˆâŸ¨_âŸ©_ : âˆ€ (x {y z} : A) â†’ x == y â†’ y == z â†’ x == z
  _ â‰ˆâŸ¨ p âŸ© q = eqr .trans p q

  _âˆ : âˆ€ (x : A) â†’ x == x
  _âˆ _ = eqr .refl

-- Setoids
record eSet {ls lr : Level} : Set (lsuc (ls âŠ” lr)) where
  no-eta-equality
  field
    set : Set ls
    rel : set â†’ set â†’ Set lr
    eqr : EqRel rel
  open EqRel eqr public

open eSet public

-- E-Categories
record ECat {lo lh lr : Level} : Set (lsuc (lo âŠ” lh âŠ” lr)) where
  no-eta-equality
  field
    obj : Set lo
    hom : obj â†’ obj â†’ Set lh
    hom-rel : âˆ€ {A B} (f g : hom A B) â†’ Set lr
    hom-eqr : âˆ€ {A B} â†’ EqRel {lh} {lr} {hom A B} hom-rel
    comp : âˆ€ {A B C} â†’ hom B C â†’ hom A B â†’ hom A C
    comp-assoc : âˆ€ {A B C D} {f : hom C D} {g : hom B C} {h : hom A B} â†’
      hom-rel (comp f (comp g h)) (comp (comp f g) h)
    comp-cong : âˆ€ {A B C} {f f' : hom B C} {g g' : hom A B} â†’
      hom-rel f f' â†’ hom-rel g g' â†’ hom-rel (comp f g) (comp f' g')
    id : âˆ€ {A} â†’ hom A A
    id-l : âˆ€ {A B} {f : hom A B} â†’ hom-rel (comp id f) f
    id-r : âˆ€ {A B} {f : hom A B} â†’ hom-rel (comp f id) f
  comp-cong-l : âˆ€ {A B C} {f f' : hom B C} {g : hom A B} â†’
    hom-rel f f' â†’ hom-rel (comp f g) (comp f' g)
  comp-cong-l p = comp-cong p (hom-eqr .refl)

  comp-cong-r : âˆ€ {A B C} {f : hom B C} {g g' : hom A B} â†’
    hom-rel g g' â†’ hom-rel (comp f g) (comp f g')
  comp-cong-r = comp-cong (hom-eqr .refl)

  comp-assoc-inv : âˆ€ {A B C D} {f : hom C D} {g : hom B C} {h : hom A B} â†’
    hom-rel (comp (comp f g) h) (comp f (comp g h))
  comp-assoc-inv = hom-eqr .sym comp-assoc
  id-l-inv : âˆ€ {A B} {f : hom A B} â†’ hom-rel f (comp id f)
  id-l-inv = hom-eqr .sym id-l
  id-r-inv : âˆ€ {A B} {f : hom A B} â†’ hom-rel f (comp f id)
  id-r-inv = hom-eqr .sym id-r



open ECat public

_op : âˆ€ {lo lh lr} â†’ ECat {lo} {lh} {lr} â†’ ECat
C op = record C
     { hom = Î» A B â†’ hom C B A
     ; comp = Î» f g â†’ comp C g f
     ; comp-assoc =  C .hom-eqr .sym (C .comp-assoc)
     ; comp-cong =  Î» p q â†’ C .comp-cong q p
     ; id-l = id-r C
     ; id-r = id-l C
     }

-- We have to pack E-Maps into a record instead of just a Î£-type;
-- otherwise, the definition of EFam below needs too many (all)
-- implicit arguments.
record eMap {lao lar lbo lbr} (A : eSet {lao} {lar}) (B : eSet {lbo} {lbr}) :
  Set (lao âŠ” lar âŠ” lbo âŠ” lbr) where
  no-eta-equality
  field
    ap : A .set â†’ B .set
    ap-cong : âˆ€ {a b} â†’ A .rel a b â†’ B .rel (ap a) (ap b)

open eMap public

record eMapRel {lao lar lbo lbr} {A : eSet {lao} {lar}} {B : eSet {lbo} {lbr}}
  (f g : eMap A B) : Set (lao âŠ” lar âŠ” lbo âŠ” lbr) where
  no-eta-equality
  constructor map-rel
  field
    map-resp : âˆ€ {a b} â†’ A .rel a b â†’ B .rel (f .ap a) (g .ap b)
  _`_ = map-resp
open eMapRel public

eMapEqR : {lao lar lbo lbr : Level} {A : eSet {lao} {lar}} {B : eSet {lbo} {lbr}} â†’
  EqRel (eMapRel {A = A} {B = B})
refl (eMapEqR {A = A} {B}) {f} = map-rel (f .ap-cong)
sym (eMapEqR {A = A} {B}) p = map-rel Î» ab â†’ B .sym (p ` (A .sym ab))
trans (eMapEqR {A = A} {B}) p q = map-rel Î» r â†’ B .trans (p ` r) (q ` (A .refl))



-- The E-Cat of E-Sets
-- TODO: this should really take two universe levels...
ESet : {l : Level} â†’ ECat {lsuc l} {l} {l}
obj (ESet {l}) = eSet {l} {l}
hom ESet A B = eMap A B
hom-rel ESet f g = eMapRel f g
hom-eqr ESet = eMapEqR
comp ESet f g = record
  { ap = Î» x â†’ f .ap (g .ap x) ; ap-cong = Î» p â†’  f .ap-cong (g .ap-cong p) }
comp-assoc ESet {f = f} {g} {h} = map-rel Î» pa â†’ f .ap-cong (g .ap-cong (h .ap-cong pa))
comp-cong ESet p q = map-rel Î» ab â†’ p ` (q ` ab)
id ESet {A} = record { ap = Î» x â†’ x ; ap-cong = Î» {_} {_} x â†’ x }
id-l ESet {f = f} = map-rel Î» ab â†’ f .ap-cong ab
id-r ESet {f = f} = map-rel Î» ab â†’ f .ap-cong ab

ESet0 : ECat
ESet0 = ESet {lzero}


-- E-Functors
record eFunctor {lco lch lcr ldo ldh ldr : Level}
    (C : ECat {lco} {lch} {lcr}) (D : ECat {ldo} {ldh} {ldr}) :
      Set (lco âŠ” lch âŠ” lcr âŠ” ldo âŠ” ldh âŠ” ldr) where
  no-eta-equality
  field
    fun : obj C â†’ obj D
    mor : âˆ€ {a b} (f : hom C a b) â†’ hom D (fun a) (fun b)
    resp : âˆ€ {a b} {f g : hom C a b} â†’
           hom-rel C f g â†’ hom-rel D (mor f) (mor g)
    id-mor : âˆ€ {a} â†’ hom-rel D (D .id) (mor {a} {a} (C .id))
    comp-mor : âˆ€ {a b c} {f : hom C b c} {g : hom C a b} â†’
                 hom-rel D (comp D (mor f) (mor g)) (mor (comp C f g))

  comp-mor-inv : âˆ€ {a b c} {f : hom C b c} {g : hom C a b} â†’
                 hom-rel D (mor (comp C f g)) (comp D (mor f) (mor g))
  comp-mor-inv = D .hom-eqr .sym comp-mor
  id-mor-inv : âˆ€ {a} â†’ hom-rel D (mor {a} {a} (C .id)) (D .id)
  id-mor-inv = D .hom-eqr .sym id-mor


open eFunctor public


_âˆ˜Func_ : {lco lch lcr ldo ldh ldr leo leh ler : Level}
  {C : ECat {lco} {lch} {lcr}} {D : ECat {ldo} {ldh} {ldr}} {E : ECat {leo} {leh} {ler}}
  (F : eFunctor D E) (G : eFunctor C D) â†’ eFunctor C E
_âˆ˜Func_ {C = C} {D = D} {E = E} F G = FG where
  FG : eFunctor C E
  fun FG =  Î» c â†’ fun F (fun G c)
  mor FG =  Î» p â†’ mor F (mor G p)
  resp FG = Î» p â†’ resp F (resp G p)
  id-mor FG = E .hom-eqr .trans (id-mor F) (resp F (id-mor G))
  comp-mor FG = E .hom-eqr .trans (comp-mor F) (resp F (comp-mor G))

-- E-Natural Transformations
record eNat {lco lch lcr ldo ldh ldr : Level}
    {C : ECat {lco} {lch} {lcr}} {D : ECat {ldo} {ldh} {ldr}}
    (F G : eFunctor C D) : Set (lco âŠ” lch âŠ” lcr âŠ” ldo âŠ” ldh âŠ” ldr) where
  no-eta-equality
  open ECat D using() renaming (comp to _âˆ˜D_ ; hom-rel to _~D_)
  field
    nat : (a : obj C) â†’ hom D (fun F a) (fun G a)
    nat-eq : âˆ€ {a b} {f : hom C a b} â†’
               ((mor G f) âˆ˜D (nat a)) ~D ((nat b) âˆ˜D (mor F f))

open eNat public


EFunctor : {lco lch lcr ldo ldh ldr : Level}
  (C : ECat {lco} {lch} {lcr}) (D : ECat {ldo} {ldh} {ldr}) â†’
  ECat
EFunctor C D = cat where
  open ECat D using () renaming (comp to _âˆ˜D_ ; hom-rel to _~D_ ; hom-eqr to Deq)
  cat : ECat
  obj cat = eFunctor C D
  hom cat F G = eNat F G
  hom-rel cat Î± Î² =  âˆ€ a â†’ nat Î± a ~D nat Î² a
  refl (hom-eqr cat) =  Î» _ â†’ Deq .refl
  sym (hom-eqr cat) p =  Î» a â†’ Deq .sym (p a)
  trans (hom-eqr cat) p q =  Î» a â†’ Deq .trans (p a) (q a)
  nat (cat .comp Î± Î²) = Î» a â†’ nat Î± a âˆ˜D nat Î² a
  nat-eq (cat .comp {F} {G} {H} Î± Î²) {a} {b} {f} = let open EqRelReason Deq in
    begin
      ((mor H f) âˆ˜D (nat (cat .comp Î± Î²) a))
    â‰ˆâŸ¨  comp-assoc D âŸ©
      ((mor H f) âˆ˜D (nat Î± a)) âˆ˜D (nat Î² a)
    â‰ˆâŸ¨ comp-cong-l D (nat-eq Î±) âŸ©
      ((nat Î± b) âˆ˜D (mor G f)) âˆ˜D (nat Î² a)
    â‰ˆâŸ¨ comp-assoc-inv D âŸ©
      (nat Î± b) âˆ˜D ((mor G f) âˆ˜D (nat Î² a))
    â‰ˆâŸ¨ comp-cong-r D (nat-eq Î²) âŸ©
      (nat Î± b) âˆ˜D ((nat Î² b) âˆ˜D (mor F f))
    â‰ˆâŸ¨ comp-assoc D âŸ©
      ((nat (cat .comp Î± Î²) b) âˆ˜D (mor F f))
    âˆ
  comp-assoc cat = Î» _ â†’ D .comp-assoc
  comp-cong cat =  Î» p q a â†’ D .comp-cong (p a) (q a)
  nat (id cat) = Î» a â†’ D .id
  nat-eq (id cat) = Deq .trans (D .id-r) (Deq .sym (D .id-l))
  id-l cat = Î» _ â†’ D .id-l
  id-r cat = Î» _ â†’ D .id-r


-- Horizontal composition (not used at the moment)
hcomp :
  {lco lch lcr ldo ldh ldr leo leh ler : Level}
  {C : ECat {lco} {lch} {lcr}} {D : ECat {ldo} {ldh} {ldr}} {E : ECat {leo} {leh} {ler}}
  {F G : eFunctor D E} {K L : eFunctor C D}
  (Î± : eNat F G) (Î² : eNat K L) â†’ eNat (F âˆ˜Func K) (G âˆ˜Func L)
hcomp {C = C} {D} {E} {F} {G} {K} {L} Î± Î² = record
  { nat = Î» (a : obj C) â†’ comp E (mor G (nat Î² a)) (nat Î± (fun K a))
  ; nat-eq =  Î» {a b f} â†’
    let eeq = hom-eqr E
        _âˆ˜E_ = comp E
        _âˆ˜D_ = comp D
        open EqRelReason eeq
    in
      begin
        (mor G (mor L f)) âˆ˜E ((mor G (nat Î² a)) âˆ˜E (nat Î± (fun K a)))
      â‰ˆâŸ¨ comp-assoc E âŸ©
        ((mor G (mor L f)) âˆ˜E (mor G (nat Î² a))) âˆ˜E (nat Î± (fun K a))
      â‰ˆâŸ¨ comp-cong-l E (comp-mor G) âŸ©
        (mor G (mor L f âˆ˜D nat Î² a)) âˆ˜E (nat Î± (fun K a))
      â‰ˆâŸ¨ comp-cong-l E (resp G (nat-eq Î²)) âŸ©
        (mor G (nat Î² b âˆ˜D mor K f)) âˆ˜E (nat Î± (fun K a))
      â‰ˆâŸ¨ comp-cong-l E (eeq .sym (comp-mor G)) âŸ©
        (mor G (nat Î² b) âˆ˜E mor G (mor K f)) âˆ˜E (nat Î± (fun K a))
      â‰ˆâŸ¨ comp-assoc-inv E âŸ©
        mor G (nat Î² b) âˆ˜E (mor G (mor K f) âˆ˜E (nat Î± (fun K a)))
      â‰ˆâŸ¨ comp-cong-r E (nat-eq Î±) âŸ©
        mor G (nat Î² b) âˆ˜E (nat Î± (fun K b) âˆ˜E mor F (mor K f))
      â‰ˆâŸ¨ comp-assoc E âŸ©
         ((mor G (nat Î² b)) âˆ˜E (nat Î± (fun K b))) âˆ˜E (mor F (mor K f))
      âˆ
  }

_*_ = hcomp

r-whisker :
  {lco lch lcr ldo ldh ldr leo leh ler : Level}
  {C : ECat {lco} {lch} {lcr}} {D : ECat {ldo} {ldh} {ldr}} {E : ECat {leo} {leh} {ler}}
  {F G : eFunctor D E}
  (Î± : eNat F G) (H : eFunctor C D) â†’ eNat (F âˆ˜Func H) (G âˆ˜Func H)
r-whisker Î± H = Î± * (id (EFunctor _ _) {H})

l-whisker :
  {lco lch lcr ldo ldh ldr leo leh ler : Level}
  {C : ECat {lco} {lch} {lcr}} {D : ECat {ldo} {ldh} {ldr}} {E : ECat {leo} {leh} {ler}}
  {G H : eFunctor C D}
  (F : eFunctor D E) (Î± : eNat G H) â†’ eNat (F âˆ˜Func G) (F âˆ˜Func H)
l-whisker F Î± = hcomp (id (EFunctor _ _) {F}) Î±


-- Any setoid is a discrete category (which is also a groupoid)
# : âˆ€ {ls lr} (A : eSet {ls} {lr}) â†’ ECat
obj (# A) = A .set
hom (# A) a b = A .rel a b
hom-rel (# A) p q = Unit {lzero}
refl (hom-eqr (# A)) = tt
sym (hom-eqr (# A)) =  Î» _ â†’ tt
trans (hom-eqr (# A)) = Î» _ _ â†’ tt
comp (# A) p q = A .trans q p
comp-assoc (# A) = tt
comp-cong (# A) = Î» _ _ â†’ tt
id (# A) = A .refl
id-l (# A) = tt
id-r (# A) = tt


#fun : âˆ€ {l} {A B} (f : hom (ESet {l}) A B) â†’ eFunctor (# A) (# B)
fun (#fun f) = f .ap
mor (#fun f) = f .ap-cong
resp (#fun f) =  Î» _ â†’ tt
id-mor (#fun f) = tt
comp-mor (#fun f) = tt



--------------------------------------------------------------------------------

isTerminal : âˆ€ {l l' l''} {C : ECat {l} {l'} {l''}} (T : obj C) â†’ Set (l âŠ” (l' âŠ” l''))
isTerminal {C = C} T = âˆ€ A â†’ Î£ Î» (f : hom C A T) â†’ âˆ€ g â†’ hom-rel C f g


--------------------------------------------------------------------------------
-- The initial category

ğŸ˜ : âˆ€ {lo lh lr} â†’ ECat {lo} {lh} {lr}
ğŸ˜ = record
  { obj = âŠ¥
  ; hom = Î» _ _ â†’ âŠ¥
  ; hom-rel = Î» _ _ â†’ âŠ¥
  ; hom-eqr = record { refl = Î» {} ; sym = Î» z â†’ z ; trans = Î» _ z â†’ z }
  ; comp = Î» _ z â†’ z
  ; comp-assoc = Î» {}
  ; comp-cong = Î» _ z â†’ z
  ; id = Î» {}
  ; id-l = Î» {}
  ; id-r = Î» {}
  }

ğŸ˜-elim : âˆ€ {lo lh lr lco lch lcr} {C : ECat {lco} {lch} {lcr}} â†’ eFunctor (ğŸ˜ {lo} {lh} {lr}) C
ğŸ˜-elim = record { fun = Î» () ; mor = Î» () ; resp = Î» () ; id-mor = Î» {} ; comp-mor = Î» {} }



--------------------------------------------------------------------------------


module eNatIsoModule
         {lco lch lcr ldo ldh ldr : Level}
         {C : ECat {lco} {lch} {lcr}} {D : ECat {ldo} {ldh} {ldr}}
         {F G : eFunctor C D} where

  open ECat C using() renaming (comp to _âˆ˜C_ ; hom-rel to _~C_)
  open ECat D using() renaming (comp to _âˆ˜D_ ; hom-rel to _~D_ ; hom-eqr to deq)

  record isNatIso (Î± : eNat F G) : Set (lco âŠ” lch âŠ” lcr âŠ” ldo âŠ” ldh âŠ” ldr) where
    no-eta-equality
    field
      nat-inv : (a : obj C) â†’ hom D (fun G a) (fun F a)
      nat-inv-sect : âˆ€ {a} â†’ (nat Î± a âˆ˜D nat-inv a) ~D id D
      nat-inv-retract : âˆ€ {a} â†’ (nat-inv a âˆ˜D nat Î± a) ~D id D

  isnatiso-inv : âˆ€ {Î±} â†’ isNatIso Î± â†’ eNat G F
  isnatiso-inv {Î±} p = let open isNatIso p in record
    { nat = nat-inv
    ; nat-eq = Î» {a b f} â†’ let open EqRelReason deq in
      begin
        mor F f âˆ˜D nat-inv a
      â‰ˆâŸ¨ comp-cong-l D (id-l-inv D) âŸ©
        (id D âˆ˜D mor F f) âˆ˜D nat-inv a
      â‰ˆâŸ¨ comp-cong-l D (comp-cong-l D (deq .sym nat-inv-retract)) âŸ©
        ((nat-inv b âˆ˜D nat Î± b) âˆ˜D mor F f) âˆ˜D nat-inv a
      â‰ˆâŸ¨ comp-cong-l D (comp-assoc-inv D) âŸ©
        (nat-inv b âˆ˜D (nat Î± b âˆ˜D mor F f)) âˆ˜D nat-inv a
      â‰ˆâŸ¨ comp-cong-l D (comp-cong-r D (deq .sym (nat-eq Î±))) âŸ©
        (nat-inv b âˆ˜D (mor G f âˆ˜D nat Î± a)) âˆ˜D nat-inv a
      â‰ˆâŸ¨ comp-assoc-inv D âŸ©
        nat-inv b âˆ˜D ((mor G f âˆ˜D nat Î± a) âˆ˜D nat-inv a)
      â‰ˆâŸ¨ comp-cong-r D (comp-assoc-inv D) âŸ©
        nat-inv b âˆ˜D (mor G f âˆ˜D (nat Î± a âˆ˜D nat-inv a))
      â‰ˆâŸ¨ comp-cong-r D (comp-cong-r D (nat-inv-sect)) âŸ©
        nat-inv b âˆ˜D (mor G f âˆ˜D id D)
      â‰ˆâŸ¨ comp-cong-r D (id-r D) âŸ©
        nat-inv b âˆ˜D mor G f
      âˆ
    }

  record eNatIso : Set (lco âŠ” lch âŠ” lcr âŠ” ldo âŠ” ldh âŠ” ldr) where
    no-eta-equality
    field
      to-nat : eNat F G
      to-is-iso : isNatIso to-nat

  open eNatIso public

open eNatIsoModule public



-- -}
